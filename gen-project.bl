#!/usr/local/bin/blc -rs

#import "std/arg_parser"
#import "std/fs"

Args :: struct #base ArgCtx {
    is_debug: bool;
    make: bool;
    tracy_enable: bool;
}

DIR :: "build";
CMAKE_CACHE :: "CMakeCache.txt";

_args: Args;

main :: fn () s32 {
    arg_parser :: arg_parser_new();
    defer arg_parser_delete(arg_parser);

    arg_parser_add(
        arg_parser,
        "-d",
        "--debug",
        "Generate project in debug mode.",
        &fn (parser: ArgParser, args: []string, ctx: *ArgCtx) (s32, Error) {
            a: *Args = auto ctx;
            a.is_debug = true;
            return 1, ok();
        });

    arg_parser_add(
        arg_parser,
        "-m",
        "--make",
        "Make project.",
        &fn (parser: ArgParser, args: []string, ctx: *ArgCtx) (s32, Error) {
            a: *Args = auto ctx;
            a.make = true;
            return 1, ok();
        });

    arg_parser_add(
        arg_parser,
        "-te",
        "--tracy-enable",
        "Enable tracy profiler (available only in debug build).",
        &fn (parser: ArgParser, args: []string, ctx: *ArgCtx) (s32, Error) {
            a: *Args = auto ctx;
            a.tracy_enable = true;
            return 1, ok();
        });

    state :: arg_parser_run(arg_parser, command_line_arguments, &_args, 1);   
    if !is_ok(state) {
        print_err("%", state);
        arg_parser_print_help(arg_parser);
        return 1;
    }
    if _args.help { return 0; }

    path :: string_new();
    defer string_delete(path);
    fs_dir_create(DIR);
    fs_remove(string_clear_concat(&path, DIR, "/", CMAKE_CACHE));
    fs_set_cwd(DIR);

    conf :: conf_name(_args.is_debug);
    print("Generate configuration '%' in '%'.\n", conf, DIR);
    cmake_generate();
    if _args.make {
        print("Build configuration '%' in '%'.\n", conf, DIR);
        cmake_compile();
    }
    return 0;
}

conf_name :: fn (is_debug: bool) string #inline {
    if is_debug { return "Debug"; }
    return "Release";
}

cmake_generate :: fn () s32 #inline {
    is_debug :: _args.is_debug;
    conf :: conf_name(is_debug);
    cmd: string;
    defer string_delete(cmd);
    if OS_KIND == OSKind.Windows {
        cmd = sprint("cmake .. -G \"Visual Studio 16 2019\" -Thost=x64 -DCMAKE_BUILD_TYPE=", conf);
    } else {
        cmd = sprint("cmake .. -DCMAKE_BUILD_TYPE=", conf);
    }
    if _args.tracy_enable {
        if !_args.is_debug {
            print_warn("Tracy profiler can be enabled only in debug mode.");
        } else {
            string_append(&cmd, " -DTRACY_ENABLE=ON");
        }
    }
    return os_execute(cmd);
}

cmake_compile :: fn () s32 #inline {
    is_debug :: _args.is_debug;
    conf :: conf_name(is_debug);
    cmd: string;
    defer string_delete(cmd);
    if OS_KIND == OSKind.Windows {
        cmd = sprint("cmake --build . --config ", conf);
    } else {
        cmd = sprint("make -j");
    }
    return os_execute(cmd);
}