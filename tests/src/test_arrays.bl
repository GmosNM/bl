#load "std/debug.bl"
#load "experimental/array.bl"

#test "simple static array" {
  arr : [10]s32;

  arr[0] = 10;
  arr[1] = 20;
  arr[2] = 30;

  assert(arr.ptr == &arr[0]);
  assert(arr.len == 10);

  assert(arr[0] == 10);
  assert(arr[1] == 20);
  assert(arr[2] == 30);
};

#test "array initialization" {
  {
    arr := {:[2]s32: 0 };
    assert(arr[0] == 0);
    assert(arr[1] == 0);
  }

  {
    arr : [2]s32 = {:[2]s32: 10, 20 };
    assert(arr[0] == 10);
    assert(arr[1] == 20);
  }

  {
    arr := {:[2]s32: 10, 20 };
    assert(arr[0] == 10);
    assert(arr[1] == 20);
  }

  {
    i := 10;
    j := 20;

    arr := {:[2]s32: i, j };
    assert(arr[0] == 10);
    assert(arr[1] == 20);
  }
};

#test "simple static array loops" {
  arr : [10]s32;
  loop i := 0; i < arr.len; i += 1 {
    arr[i] = i;
    assert(arr[i] == i);
  }

  assert(arr[0] == 0);
  assert(arr[9] == 9);
};

#test "dynamic array init" {
    arr := array_new(s32);
    assert(arr.len == 0);
    assert(arr.data == null);
    assert(arr.allocated == 0);
    assert(arr.T == typeinfo(s32));

    array_reserve(arr, 256);
    assert(arr.len == 0);
    assert(arr.data != null);
    assert(arr.allocated == 256);
    
    array_delete(arr);
};

#test "dynamic array data" {
    arr := array_new(s32);
    
    loop i := 0; i < 1024; i += 1 {
        array_push(arr, i);
    }

    loop i := 0; i < 1024; i += 1 {
        v := ^cast(*s32) array_at(arr, i);
        assert(v == i);
    }
    
    array_clear(arr);
    assert(arr.len == 0);
    assert(arr.data != null);

    array_delete(arr);
};

#test "dynamic array data struct" {
    Foo :: struct {
        i: s32, j: s32, k: s32
    };

    arr := array_new(Foo);
    
    loop i := 0; i < 1024; i += 1 {
        v := {:Foo: i, i, i};
        array_push(arr, v);
    }

    loop i := 0; i < 1024; i += 1 {
        v := ^cast(*Foo) array_at(arr, i);
        assert(v.i == i);
        assert(v.j == i);
        assert(v.k == i);
    }
    
    array_clear(arr);
    assert(arr.len == 0);
    assert(arr.data != null);

    array_delete(arr);
};