#private
compound_arrays :: fn () #test {
    validate :: fn (v: [2]s32, i: s32, j: s32) {
        assert(v[0] == i);
        assert(v[1] == j);
    };

    // Var initializers
    { // zero initialized
        arr := {:[2]s32: 0};
        validate(arr, 0, 0);
    }

    { // constant values
        arr := {:[2]s32: 10, 20};
        validate(arr, 10, 20);
    }

    { // variable values
        i := 10;
        j := 20;

        arr := {:[2]s32: i, j};
        validate(arr, i, j);
    }

    // Naked
    { 
        i := 10;
        j := 20;

        validate({:[2]s32: 0}, 0, 0);
        validate({:[2]s32: 10, 20}, 10, 20);
        validate({:[2]s32: i, j}, i, j);
    }
};

compound_structs :: fn () #test {
    Foo :: struct {
        i: s32;
        j: s32
    };

    validate :: fn (v: Foo, i: s32, j: s32) {
        assert(v.i == i);
        assert(v.j == j);
    };

    { // zero initialized
        foo := {:Foo: 0};
        validate(foo, 0, 0);
    }

    { // constant values
        foo := {:Foo: 10, 20};
        validate(foo, 10, 20);
    }

    { // variable values
        i := 10;
        j := 20;

        foo := {:Foo: i, j};
        validate(foo, i, j);
    }

    // Naked
    { 
        i := 10;
        j := 20;

        validate({:Foo: 0}, 0, 0);
        validate({:Foo: 10, 20}, 10, 20);
        validate({:Foo: i, j}, i, j);
    }
};

compound_fundamentals :: fn () #test {
    Days :: enum { MON; };
    
    i := 10;
    assert({:s32: 10} == 10);
    assert({:s32: i} == i);
    assert({:s32: 0} == 0);

    b := true;
    assert({:bool: true} == true);
    assert({:bool: b} == b);

    num     :: {:s32: 10};
    real    :: {:f32: 0.5f};
    boolean :: {:bool: true};
    ptr     : *s32 : {:*s32: &num};
    day     :: {:Days: Days.MON};

    assert(num == 10);
    assert(real == 0.5f);
    assert(boolean == true);
    assert(ptr == &num);
    assert(day == Days.MON);
};

compound_composition :: fn () #test {
    get :: fn () s32 { return 30; };
    T :: struct { slice: []u8; int: s32 };
    v :: {:T: {:[]u8: 0}, get()};
    test_eq(v.int, 30);    
    test_eq(v.slice.len, 30);
}