// =================================================================================================
// bl
//
// File:   utils.bl
// Author: Martin Dorazil
// Date:   4/9/19
//
// Copyright 2019 Martin Dorazil
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.
// =================================================================================================

//! =====
//! Utils
//! =====
//! 
//! ::
//! 
//!     #load "std/utils.bl"
//! 
//! Set of various utility functions.

/// Set `flag` in `flags` input. This function is valid for numeric and enum types (checked by assert).
set_flag :: fn (flags: *?T, flag: T) T #inline {
    assert(flags);
    is_enum :: typeinfo(T).kind == TypeKind.Enum;
    #if is_enum {
        // @Incomplete: Check enums for #flags tag.
        switch sizeof(T) {
            1 { f := cast(*u8)flags;  @f |= cast(u8) flag;  }
            2 { f := cast(*u16)flags; @f |= cast(u16) flag; }
            4 { f := cast(*u32)flags; @f |= cast(u32) flag; }
            8 { f := cast(*u64)flags; @f |= cast(u64) flag; }
            default { panic("Unsupported type in 'set_flag' %.", T); }
        }
    } else {
        #assert(typeinfo(T).kind == TypeKind.Int)
        @flags |= flag;
    }
    return @flags;
}

/// Clear `flag` in `flags` input. This function is valid for numeric and enum types (checked by assert).
clr_flag :: fn (flags: *?T, flag: T) T #inline {
    assert(flags);
    is_enum :: typeinfo(T).kind == TypeKind.Enum;
    #if is_enum {
        // @Incomplete: Check enums for #flags tag.
        switch sizeof(T) { 
            1 { f := cast(*u8)flags;  @f &= ~cast(u8) flag;  }
            2 { f := cast(*u16)flags; @f &= ~cast(u16) flag; }
            4 { f := cast(*u32)flags; @f &= ~cast(u32) flag; }
            8 { f := cast(*u64)flags; @f &= ~cast(u64) flag; }
            default { panic("Unsupported type in 'clr_flag' %.", T); }
        } 
    } else {
        #assert(typeinfo(T).kind == TypeKind.Int)
        @flags &= ~flag;
    }
    return @flags;
}

/// Check whether `flag` is set in `flags`. This function is valid for numeric and enum types (checked by assert).
is_flag :: fn (flags: ?T, flag: T) bool #inline {
    result := false;
    is_enum :: typeinfo(T).kind == TypeKind.Enum;
    #if is_enum {
        // @Incomplete: Check enums for #flags tag.
        switch sizeof(T) {
            1 { result = (cast(u8)flags & cast(u8)flag) == cast(u8) flag;     }
            2 { result = (cast(u16)flags & cast(u16)flag) == cast(u16) flag;  }
            4 { result = (cast(u32)flags & cast(u32)flag) == cast(u32) flag;  }
            8 { result = (cast(u64)flags & cast(u64)flag) == cast(u64) flag;  }
            default { panic("Unsupported type in 'is_flag' %.", T); }
        }
    } else {
        #assert(typeinfo(T).kind == TypeKind.Int)
        result = (flags & flag) == flag;        
    }
    return result;
}

/// Converts zero terminated C style string to BL string. Result
/// string shares original memory of `cstr`.
cstr_to_str :: fn (cstr: *u8) string #inline {
    return {:string: auto c_strlen(auto cstr), cstr};
};

/// Produce right-shift of input `ptr` by count of `bytes`.
ptr_shift_bytes :: fn (ptr: *?T, bytes: s64) *u8 #inline {
    return cast(*u8) (cast(s64) ptr + bytes);
};

/// Calculates pointer difference `a` - `b`.
ptr_diff :: fn (a: *?T1, b: *?T2) s64 #inline {
    return (cast(s64) a) - (cast(s64) b); 
}

/// Reads environemnt variable specified by `var` name. Result is empty
/// in case no such variable was found or has no content. It's caller
/// responsibility to delete result string.
env_get :: fn (var: string) string {
    out :: cast(*u8) c_getenv(auto var.ptr);
    if !out { return String.new(); }
    return String.new(out);
}

/// Sets environment variable.
env_set :: fn (var: string) #inline {
    c_putenv(auto var.ptr);
}

/// Sets seed for :ref:`Math.rand` or utility function :ref:`random_number` based
/// on current system tick time.
random_seed_time :: fn () #inline {
    Math.srand(auto os_tick_ms());
}

/// Generates random number in specified range <min, max> using standart
/// libc rand generator.
/// Random number generator seed is supposed to be set by :ref:`random_seed_time`
/// or by :ref:`Math.srand` call.
random_number :: fn (min := 0, max := 1) s32 #inline {
    return Math.rand() % (max + 1 - min) + min;
}

/// Polymorph slice sorting utility.
/// @INCOMPLETE
sort :: fn (list: []?T, cmp: *fn(a: *T, b: *T) bool) {
    assert(cmp);
    loop i := 0; i < list.len; i += 1 {
        loop j := i+1; j < list.len; j += 1 {
            if cmp(&list[i], &list[j]) {
                swap(&list[i], &list[j]);
            }
        }
    }
}
