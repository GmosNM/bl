//***********************************************************************************************t
// bl
//
// File:   debug.bl
// Author: Martin Dorazil
// Date:   3/15/18
//
// Copyright 2018 Martin Dorazil
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.
//************************************************************************************************

/*!
# print_log
## Declaration
```c
print_log :: fn (format: string, args: ...)
```

## Description
  Print debug log using `_context.print_log_fn` function. 

## Arguments
  * `format`
    Formating string accepting `%` character as value placeholder.
  * `args`
    Optional values passed in order which is supposed to be printed
    out.
*/
print_log :: fn (format: string, args: ...) {
    tmp := {:[]Any: args.len, args.ptr };
    _context.print_log_fn(PrintLogKind.Message, format, tmp, #file, #line);
};

/*!
# print_warn
## Declaration
```c
print_warn :: fn (format: string, args: ...)
```

## Description
  Print debug warning using `_context.print_log_fn` function. 

## Arguments: 
  * `format`
    Formating string accepting '%' character as value placeholder.
  * `args`
    Optional values passed in order which is supposed to be printed
    out.
 */
print_warn :: fn (format: string, args: ...) {
    tmp := {:[]Any: args.len, args.ptr };
    _context.print_log_fn(PrintLogKind.Warning, format, tmp, #file, #line);
};


/*!
# print_err
## Declaration
```c
print_err :: fn (format: string, args: ...)
```

## Description
  Print debug error using _context.print_log_fn function. 

## Arguments
  * `format`
    Formating string accepting `%` character as value placeholder.
  * `args`
    Optional values passed in order which is supposed to be printed
    out.
*/
print_err :: fn (format: string, args: ...) {
    tmp := {:[]Any: args.len, args.ptr };
    _context.print_log_fn(PrintLogKind.Error, format, tmp, #file, #line);
};

/*!
# assert
## Declaration
```c
assert :: fn (cond: bool)
```

## Description
  Abort execution when passed condition is resulting in `false`
  value.

## Arguments
  * `cond`
    Condition to be validated. 

*/
assert :: fn (cond: bool, loc: *CodeLocation = #call_location) {
    if cond { return; }
    eprint(">>> Assertion failed on %:%.\n", loc.file, loc.line);
    if IS_DEBUG { _os_debug_break(); } // @PERFORMANCE: cound be static if later
    unreachable;
};

// @DOC
debug_break :: fn () #inline {
    _os_debug_break();
}

/*!
# panic
## Declaration
```c
panic :: fn (args: ...)
```

## Description
  Abort execution and eventually print panic message if there is one specified. First
passed argument in 'args' will act like format string and can be eventually followed
by any additional values required.

## Arguments
  * `args`
    Variable argument count of Any type. 

## Example
```c
panic(); // abort without any messages
panic("oops!"); // abort with message prited out.
panic("Failed with error: %", errn); // first argument passed acts like formating string
```
*/
panic :: fn (args: ...) {
    if args.len == 0 { unreachable; }
    tmp := {:[]Any: args.len, args.ptr };
    format := "";
    arg := args[0];
    if arg.type_info.kind == TypeKind.String {
        format = ^cast(*string) arg.data;
        tmp = {:[]Any:
                args.len - 1,
                auto ptr_shift_bytes(auto args.ptr, auto sizeof(arg))};
    }
    _context.print_log_fn(PrintLogKind.Panic, format, tmp, "", 0);
    unreachable;
};

/*!
# PrintLogKind
## Declaration
```c
PrintLogKind :: enum u8 {
    Message;
    Warning;
    Error;
    Panic;
};
```
## Description
  Kinds of log messages.

## Variants
  * `Message` Ordinary debug log message.
  * `Warning` Warning log message.
  * `Error` Error log message.
  * `Panic` Panic log message.
*/
PrintLogKind :: enum u8 {
    Message;
    Warning;
    Error;
    Panic;
};

_print_log_default :: fn (kind: PrintLogKind, format: string, args: []Any, file: string, line: s32) {
    out := OS_STDOUT;
    buf: [PRINT_MAX_LENGTH]u8 #noinit;
    tag: string;
    w: s32;

    switch kind {
        PrintLogKind.Message { tag = "[MESSAGE] %:% > "; }
        PrintLogKind.Warning { tag = "[WARNING] %:% > "; }
        PrintLogKind.Error   { out = OS_STDERR; tag = "[ ERROR ] %:% > "; }
        PrintLogKind.Panic   { out = OS_STDERR;  tag = "[ PANIC ] %:% > "; }
    }

    w = bprint(buf, tag, file, line);
    w = _print_impl(buf, format, args, w);
    w = print_char(buf, w, '\n');
    __os_write(out, buf.ptr, auto w);
};

