#import "std/io"
#load "fs.bl"

#scope _fs_impl

File :: s64;
FILE_INVALID : File : -1;

exist :: fn (filepath: string_view) bool #inline {
    if filepath.len == 0 { return false; }
    tmp: C.stat_t;
    return C.stat(std.strtoc(filepath), &tmp) == 0;
}

open :: fn (filepath: string_view, mode: std.OpenFileMode) (File, Error) {
    if std.str_is_empty(filepath) { return 0, error(ERR_INVALID_HANDLE, "File path is empty!"); }
    flags := modes_to_flags(mode);
    mode_flags :: C.S_IRUSR | C.S_IWUSR | C.S_IROTH | C.S_IWOTH;
    handle :: C.open(std.strtoc(filepath), flags, mode_flags);
    if handle < 0 { // error
        ec, estr :: os_get_last_error();
        return 0, error(ec, "Cannot open file '%': %.", filepath, estr);
    }    
    return handle, OK;   
}

remove :: fn (filepath: string_view) Error #inline {
    if std.str_is_empty(filepath) { return error(ERR_INVALID_HANDLE, "File path is empty!"); }
    if C.unlink(std.strtoc(filepath)) != 0 {
        ec, estr :: os_get_last_error();
        return error(ec, "Cannot remove file '%': %.", filepath, estr);
    }
    return OK;
}

copy :: fn (src: string_view, dest: string_view, override: bool) Error {
    if std.str_is_empty(src) { return error(ERR_INVALID_HANDLE, "File path is empty!"); }
    if std.str_is_empty(dest) { return error(ERR_INVALID_HANDLE, "File path is empty!"); }
    if exist(dest) && !override { return error(ERR_ALREADY_EXIST, "Destination already exist '%'", dest); }
    file_src, err1 :: open(src, std.OpenFileMode.READ);
    defer close(file_src);
    if err1 { return err1; }
    tmp, err2 :: std.read_slice(file_src);
    defer slice_terminate(&tmp);
    if err2 { return err2; }
    file_dest, err3:: open(dest, make_flags(std.OpenFileMode.WRITE, std.OpenFileMode.CREATE));
    defer close(file_dest);
    if err3 { return err3; }
    _, err4 :: std.write_slice(file_dest, tmp);
    return err4;
}

close :: fn (handle: File) #inline {
    C.close(auto handle);
}

get_uid :: fn (handle: File) (u64, Error) {
    tmp: C.stat_t;
    if C.fstat(auto handle, &tmp) != 0 {
        ec, estr :: os_get_last_error();
        return 0, error(ec, "Cannot get file information.", estr);
    }
    return auto tmp.st_ino, OK;
}

get_uid_by_name :: fn (filepath: string_view) (u64, Error) {
    file, err1 :: open(filepath, std.OpenFileMode.READ);
    defer close(file);
    if err1 { return 0, err1; }
    return get_uid(file);
}

size :: fn (handle: File) (usize, Error) #inline {
    tmp: C.stat_t;
    if C.fstat(auto handle, &tmp) != 0 {
        ec, estr :: os_get_last_error();
        return 0, error(ec, "Cannot get file size: %.", estr);
    }
    return auto tmp.st_size, OK;
}

create_dir :: fn (dirpath: string_view) Error {
    if std.str_is_empty(dirpath) { return error(ERR_INVALID_HANDLE, "Cannot create directory, dirpath is empty."); }
    if C.mkdir(std.strtoc(dirpath), C.S_IRWXU | C.S_IRWXG | C.S_IROTH | C.S_IXOTH) != 0 {
        ec, estr :: os_get_last_error();
        return error(ec, "Cannot create directory '%': %.", dirpath, estr);
    }
    return OK;
}

remove_dir :: fn (dirpath: string_view) Error {
    if std.str_is_empty(dirpath) { return error(ERR_INVALID_HANDLE, "Cannot remove directory, dirpath is empty."); }
    if C.rmdir(std.strtoc(dirpath)) != 0 {
        ec, estr :: os_get_last_error();
        return error(ec, "Cannot remove directory '%': %.", dirpath, estr);
    }
    return OK;
}

visit_files :: fn (dirpath: string_view, is_recursive: bool, leaf_first: bool, visitor: *fn(info: *std.Info, ctx: *u8) Error, ctx: *u8 = null) Error #inline {
    using std;
    directory := str_new(dirpath);
    defer str_delete(&directory);
    err :: normalize(&directory);
    if err { return err; }

    dir := C.opendir(strtoc(directory));
    if !dir {
        ec, estr :: os_get_last_error();
        return error(ec, "Cannot list directory '%': %.", dirpath, estr);
    }
    defer C.closedir(dir);

    filepath :: str_new();
    defer str_delete(&filepath);
    loop {
        stat :: C.readdir(dir);
        if stat == null { break; }
        info: Info;
        info.is_directory = stat.d_type == auto C.DT_DIR;
        info.filename = ctostr(stat.d_name.ptr);
        if (!str_match(info.filename, ".")) && (!str_match(info.filename, "..")) {
            str_clear_concat(&filepath, directory, "/", info.filename);
            info.filepath = filepath;
            if !leaf_first {
                visit_err :: visitor(&info, ctx);
                if visit_err { return visit_err; }
            }
            if is_recursive && info.is_directory {
                err :: _fs_impl.visit_files(filepath, is_recursive, leaf_first, visitor, ctx);
                if err { return err; }
            }
            if leaf_first {
                visit_err :: visitor(&info, ctx);
                if visit_err { return visit_err; }
            }
        }
    }
    return OK;
}

is_directory :: fn (path: string_view) (bool, Error) #inline {
    if std.str_is_empty(path) { return false, error(ERR_INVALID_HANDLE, "Path is empty."); }
    stat: C.stat_t #noinit;
    if C.stat(std.strtoc(path), &stat) != 0 {
        ec, estr :: os_get_last_error();
        return false, error(ec, "Cannot check directory '%': %.", path, estr);
    }
    return C.S_ISDIR(stat.st_mode), OK;
}

validate_filename :: fn (name: string_view) bool {
    if name.len == 0 { return false; }
    invalid :: "/";
    loop i := 0; i < invalid.len; i += 1 {
        if std.str_count_of(name, invalid[i]) > 0 { return false; }
    }
    return true;
}

get_cwd :: fn () string #inline {
    buf: [OS_PATH_MAX]u8;
    if !C.getcwd(auto buf.ptr, auto buf.len) { return std.str_new(); }
    path :: std.str_new(buf.ptr);
    return remove_last_separator(path);
}

set_cwd :: fn (path: string_view) Error #inline {
    if std.str_is_empty(path) { return error(ERR_INVALID_HANDLE, "Path is empty!"); }
    if C.chdir(std.strtoc(path)) != 0 {
        ec, estr :: os_get_last_error();
        return error(ec, "Cannot set current directory to '%': %.", path, estr);
    }
    return OK;
}

get_home :: fn () string #inline {
    tmp :: "HOME";
    cdir :: cast(*u8) C.getenv(auto tmp.ptr);
    if !cdir { return std.str_new(); }
    path :: std.str_new(cdir);
    return remove_last_separator(path);
}

get_tmp :: fn () string #inline {
    tmp :: "TMPDIR";
    cdir :: cast(*u8) C.getenv(auto tmp.ptr);
    if !cdir { return std.str_new("/tmp"); }
    path :: std.str_new(cdir);
    return remove_last_separator(path);
}

normalize :: fn (filepath: *string) Error {
    if !filepath { return error(ERR_INVALID_HANDLE, "File path is null!"); }
    if std.str_is_empty(@filepath) { return error(ERR_INVALID_HANDLE, "File path is empty!"); }
    buf : [OS_PATH_MAX]u8;
    in_buf : [OS_PATH_MAX]u8;

    if !is_path_absolute(@filepath) {
        cwd :: get_cwd();
        defer std.str_delete(&cwd);
        bprint(in_buf, "%/%", cwd, @filepath);
    } else {
        bprint(in_buf, "%", @filepath);
    }
    if !C.realpath(auto in_buf.ptr, auto buf.ptr) {
        ec, estr :: os_get_last_error();
        return error(ec, "Cannot normalize '%': %.", @filepath, estr);
    }
    std.str_clear(filepath);
    std.str_append(filepath, std.ctostr(auto buf.ptr));
    return OK;
}

#private
modes_to_flags :: fn (modes: std.OpenFileMode) s32 #inline {
    r, w, create, append := false;
    if is_flag(modes, std.OpenFileMode.READ) { r = true; }
    if is_flag(modes, std.OpenFileMode.WRITE) { w = true; }
    if is_flag(modes, std.OpenFileMode.APPEND) { w = true; append = true; }
    if is_flag(modes, std.OpenFileMode.CREATE) { create = true; }
    flags: s32;
    if ((!w) && (!r)) { flags = C.O_RDONLY; }
    else if (w && r) { flags = C.O_RDWR; }
    else if w { flags = C.O_WRONLY; }
    else if r { flags = C.O_RDONLY; }
    if create { flags = flags | C.O_CREAT; }
    if append { flags = flags | C.O_APPEND; }
    if w && !append { flags = flags | C.O_TRUNC; }
    return flags;
}

remove_last_separator :: fn (path: string) string {
    if std.str_is_empty(path) { return path; }
    if path[path.len-1] == '/' {
        path[path.len-1] = '\0';
        path.len -= 1;
    }
    return path;
}

is_path_absolute :: fn (path: string) bool #inline {
    return path.len > 0 && path[0] == '/';
}
