#import "std/test"
#import "std/debug_allocator"

#import "std/fs"

main :: fn () s32 {
    DebugAllocator.init();
    //DebugAllocator.break_on(6);
    defer DebugAllocator.terminate();
    return test_run();    
}

#private
get_dummy_filepath :: fn (i := 0) string {
    tmp :: Fs.get_tmp();
    String.concatenate(&tmp, "/deleteme_", i);
    return tmp;
}

fs_exist_test :: fn () #test {
    test_true(Fs.exist(#file));
    test_false(Fs.exist("foobar"));
}

fs_get_home_test :: fn () #test {
    tmp :: Fs.get_home();
    defer String.delete(tmp);
    test_neq(tmp, String.empty);
    test_true(Fs.exist(tmp));
    test_true(tmp[tmp.len-1] != '/');

    state, err :: Fs.is_directory(tmp);
    test_ok(err);
    test_true(state);
}

fs_get_tmp_test :: fn () #test {
    tmp :: Fs.get_tmp();
    defer String.delete(tmp);
    test_neq(tmp, String.empty);
    test_true(Fs.exist(tmp));    
    test_true(tmp[tmp.len-1] != '/');

    state, err :: Fs.is_directory(tmp);
    test_ok(err);
    test_true(state);
}

fs_get_cwd_test :: fn () #test {
    tmp :: Fs.get_cwd();
    defer String.delete(tmp);
    test_neq(tmp, String.empty);
    test_true(Fs.exist(tmp));    
    test_true(tmp[tmp.len-1] != '/');

    state, err :: Fs.is_directory(tmp);
    test_ok(err);
    test_true(state);
}

fs_open_test1 :: fn () #test {
    file, err :: Fs.open("");
    defer Fs.close(file);
    test_is_error(err, ERR_INVALID_HANDLE);    
}
    
fs_open_test2 :: fn () #test {
    file, err :: Fs.open("foobar");
    defer Fs.close(file);
    test_is_error(err, ERR_NOT_FOUND);
}
    
fs_open_test3 :: fn () #test {
    file, err :: Fs.open(#file);    
    defer Fs.close(file);
    test_ok(err);

    // Cannot write to file when Write or Append is not specified.
    written, err2 :: Fs.write_string(file, "Hello!");
    test_not_ok(err2);
    test_eq(written, 0);
}

fs_file_create_test1 :: fn () #test {
    file, err :: Fs.open("", Fs.OpenMode.Create);
    defer Fs.close(file);
    test_is_error(err, ERR_INVALID_HANDLE);    
}

fs_file_create_test2 :: fn () #test {
    filepath :: get_dummy_filepath();
    defer String.delete(filepath);
    Fs.remove(filepath);

    {
        file, err :: Fs.open(filepath, Fs.OpenMode.Create);
        defer Fs.close(file);
        test_ok(err);

        // Cannot write to file when Write or Append is not specified.
        written, err2 :: Fs.write_string(file, "Hello!");
        test_not_ok(err2);
        test_eq(written, 0);
    }    

    {
        file, err :: Fs.open(filepath, Fs.OpenMode.Create, Fs.OpenMode.Write);
        defer Fs.close(file);
        test_ok(err);

        written, err2 :: Fs.write_string(file, "Hello!");
        test_ok(err2);
        test_neq(written, 0);
    }    

    { // This should reuse already created file!
        file, err :: Fs.open(filepath, Fs.OpenMode.Create);
        defer Fs.close(file);
        test_ok(err);
        str :: Fs.read_string(file);
        defer String.delete(str);
        test_eq(str, "Hello!");
    }
    Fs.remove(filepath);
}

fs_create_append :: fn () #test {
    filepath :: get_dummy_filepath();
    defer String.delete(filepath);
    Fs.remove(filepath);
    { // initial create
        file, err :: Fs.open(filepath, Fs.OpenMode.Create, Fs.OpenMode.Write);
        defer Fs.close(file);
        test_ok(err);
        Fs.write_string(file, "Hello!");
    }
    { // append
        file, err :: Fs.open(filepath, Fs.OpenMode.Append);
        defer Fs.close(file);
        test_ok(err);
        Fs.write_string(file, "Hello!");
    }
    { // check
        file, err :: Fs.open(filepath);
        defer Fs.close(file);
        test_ok(err);
        str, err2 :: Fs.read_string(file);
        defer String.delete(str);
        test_ok(err2);
        test_eq(str, "Hello!Hello!");
    }
    Fs.remove(filepath);
}

fs_get_uid_test :: fn () #test {
    filepath :: get_dummy_filepath();
    defer String.delete(filepath);
    Fs.remove(filepath);
    file, err :: Fs.open(filepath, Fs.OpenMode.Create);
    defer Fs.close(file);
    test_ok(err);
    uid, err2 :: Fs.get_uid(file);
    test_ok(err2);
    test_true(uid > 0);
}

fs_get_uid_test2 :: fn () #test {
    filepath :: get_dummy_filepath();
    defer String.delete(filepath);
    Fs.remove(filepath);
    file, err :: Fs.open(filepath, Fs.OpenMode.Create);
    Fs.close(file);
    test_ok(err);
    uid, err2 :: Fs.get_uid(filepath);
    test_ok(err2);
    test_true(uid > 0);
}

fs_read_string_test :: fn () #test {
    file, err1 :: Fs.open(#file, Fs.OpenMode.Read);
    defer Fs.close(file);
    test_ok(err1);
    data, err2 := Fs.read_string(file);
    test_ok(err2);
    defer String.delete(data);
    test_neq(data, String.empty);
    data.len += 1;
    test_true(data[data.len-1] == '\0');
}

fs_read_string_test2 :: fn () #test {
    data, err := Fs.read_string(Fs.FILE_INVALID);
    test_is_error(err, ERR_INVALID_HANDLE);
}

fs_read_slice_test :: fn () #test {
    file, err1 :: Fs.open(#file, Fs.OpenMode.Read);
    test_ok(err1);
    defer Fs.close(file);
    data, err2 := Fs.read_slice(file);
    test_ok(err2);
    defer slice_terminate(&data);
    test_not_null(data.ptr);
    test_neq(data.len, 0);
}

fs_validate_filename_test :: fn () #test {
    if PLATFORM == Platform.windows {
        test_false(Fs.validate_filename("<"));
        test_false(Fs.validate_filename(">"));
        test_false(Fs.validate_filename(":")); 
        test_false(Fs.validate_filename("\""));     
        test_false(Fs.validate_filename("/"));
        test_false(Fs.validate_filename("\\"));
        test_false(Fs.validate_filename("|")); 
        test_false(Fs.validate_filename("?"));        
        test_false(Fs.validate_filename("*"));
        test_false(Fs.validate_filename("\t"));
        test_false(Fs.validate_filename("foo<"));
        test_true(Fs.validate_filename("foo_bar-File.txt"));
    } else {
        test_false(Fs.validate_filename("foo/"));
        test_true(Fs.validate_filename("foo_bar-File.txt"));
    }
}

fs_get_extension_test :: fn () #test {
    {
        filename :: "foo.bar";
        ext      :: Fs.get_extension(filename);
        test_eq(ext, "bar");
    }
    {
        filename :: "foo.bar.bar";
        ext      :: Fs.get_extension(filename);
        test_eq(ext, "bar.bar");
    }
    {
        filename :: "";
        ext      :: Fs.get_extension(filename);
        test_eq(ext, String.empty);
    }    
    {
        filename :: ".";
        ext      :: Fs.get_extension(filename);
        test_eq(ext, String.empty);
    }
    {
        filename :: ".foo";
        ext      :: Fs.get_extension(filename);
        test_eq(ext, String.empty);
    }
    {
        filename :: ".foo.bar";
        ext      :: Fs.get_extension(filename);
        test_eq(ext, "bar");
    }
    {
        filename :: ".foo.bar.bar";
        ext      :: Fs.get_extension(filename);
        test_eq(ext, "bar.bar");
    }        
}

fs_remove_extension_test :: fn () #test {
    {
        filename :: "foo.bar";
        ext      :: Fs.remove_extension(filename);
        test_eq(ext, "foo");
    }
    {
        filename :: "foo.bar.bar";
        ext      :: Fs.remove_extension(filename);
        test_eq(ext, "foo");
    }
    {
        filename :: "";
        ext      :: Fs.remove_extension(filename);
        test_eq(ext, String.empty);
    }    
    {
        filename :: ".";
        ext      :: Fs.remove_extension(filename);
        test_eq(ext, ".");
    }
    {
        filename :: ".foo";
        ext      :: Fs.remove_extension(filename);
        test_eq(ext, ".foo");
    }
    {
        filename :: ".foo.bar";
        ext      :: Fs.remove_extension(filename);
        test_eq(ext, ".foo");
    }
    {
        filename :: ".foo.bar.bar";
        ext      :: Fs.remove_extension(filename);
        test_eq(ext, ".foo");
    }
}

fs_normalize_test1 :: fn () #test {
    cwd :: Fs.get_cwd();
    defer String.delete(cwd);
    parent: string;
    test_true(String.split_by_last(cwd, '/', &parent, null));
    filepath :: String.new("..");
    defer String.delete(filepath);
    status :: Fs.normalize(&filepath);
    test_ok(status);
    test_eq(filepath, parent);
}

fs_normalize_test2 :: fn () #test {
    cwd :: Fs.get_cwd();
    defer String.delete(cwd);
    filepath :: String.new(".");
    defer String.delete(filepath);
    status :: Fs.normalize(&filepath);
    test_ok(status);
    test_eq(filepath, cwd);
}    

fs_normalize_test3 :: fn () #test {
    file :: Fs.open("foo", Fs.OpenMode.Create);
    Fs.close(file);
    defer Fs.remove("foo");
    cwd :: Fs.get_cwd();
    defer String.delete(cwd);    
    filepath :: String.new("foo");
    String.concatenate(&cwd, "/", filepath);
    defer String.delete(filepath);
    status :: Fs.normalize(&filepath);
    test_ok(status);
    test_eq(filepath, cwd);
}

fs_remove_test :: fn () #test {
    filepath :: get_dummy_filepath();
    defer String.delete(filepath);
    
    Fs.remove(filepath);
    file, err :: Fs.open(filepath, Fs.OpenMode.Create);
    test_ok(err);
    Fs.close(file);
    err2 :: Fs.remove(filepath);
    test_ok(err2);
}

fs_size_test :: fn () #test {
    file :: Fs.open(#file);
    defer Fs.close(file);
    size, err :: Fs.size(file);
    test_ok(err);
    test_true(size > 0);
}

fs_read_test :: fn () #test {
    file :: Fs.open(#file);
    defer Fs.close(file);
    buf: [1042]u8 #noinit;
    bytes, err :: Fs.read(file, buf.ptr, buf.len);
    test_ok(err);
    test_eq(bytes, buf.len);
}

fs_read_buffer_test2 :: fn () #test {
    buf: [1042]u8 #noinit;
    bytes, err :: Fs.read(Fs.FILE_INVALID, buf.ptr, buf.len);
    test_is_error(err, ERR_INVALID_HANDLE);
    test_eq(bytes, 0);
}

fs_read_buffer_test3 :: fn () #test {
    file :: Fs.open(#file);
    defer Fs.close(file);
    bytes, err :: Fs.read(file, null, 0);
    test_is_error(err, ERR_INVALID_HANDLE);
    test_eq(bytes, 0);
}

fs_read_test4 :: fn () #test {
    file :: Fs.open(#file);
    defer Fs.close(file);
    buf: [1024]u8 #noinit;
    bytes, err :: Fs.read(file, buf.ptr, 100);
    test_ok(err);
    test_eq(bytes, 100);
}

fs_write_test :: fn () #test {
    filepath :: get_dummy_filepath();
    defer String.delete(filepath);
    Fs.remove(filepath);
    content :: "Hello I'm cool file content!";    
    
    { // write
        file :: Fs.open(filepath, Fs.OpenMode.Create, Fs.OpenMode.Write);
        written, err :: Fs.write(file, content.ptr, content.len);
        test_ok(err);
        test_eq(written, content.len);
        Fs.close(file);
    }
    { // read
        file :: Fs.open(filepath);
        tmp, err :: Fs.read_string(file);
        test_ok(err);
        defer String.delete(tmp);
        test_eq(tmp, content);
        Fs.close(file);
    }
    Fs.remove(filepath);
}

fs_write_string_test :: fn () #test {
    filepath :: get_dummy_filepath();
    defer String.delete(filepath);
    Fs.remove(filepath);
    content :: "Hello I'm cool file content!";    
    
    { // write
        file :: Fs.open(filepath, Fs.OpenMode.Create, Fs.OpenMode.Write);
        written, err :: Fs.write_string(file, content);
        test_ok(err);
        test_eq(written, content.len);
        Fs.close(file);
    }
    { // read
        file :: Fs.open(filepath);
        tmp, err :: Fs.read_string(file);
        test_ok(err);
        defer String.delete(tmp);
        test_eq(tmp, content);
        Fs.close(file);
    }
    Fs.remove(filepath);
}

fs_write_slice_test :: fn () #test {
    filepath :: get_dummy_filepath();
    defer String.delete(filepath);
    Fs.remove(filepath);
    content :: {:[5]u8: 'H', 'e', 'l', 'o', 'o' };
    
    { // write
        file :: Fs.open(filepath, Fs.OpenMode.Create, Fs.OpenMode.Write);
        written, err :: Fs.write_slice(file, content);
        test_ok(err);
        test_eq(written, content.len);
        Fs.close(file);
    }
    { // read
        file :: Fs.open(filepath);
        tmp, err :: Fs.read_string(file);
        test_ok(err);
        defer String.delete(tmp);
        test_eq(tmp.len, content.len);
        Fs.close(file);
    }
    Fs.remove(filepath);
}

fs_copy_test :: fn () #test {
    filepath1 :: get_dummy_filepath(1);
    filepath2 :: get_dummy_filepath(2);
    defer String.delete(filepath1);
    defer String.delete(filepath2);
    Fs.remove(filepath1);
    Fs.remove(filepath2);
    
    { // new file
        file :: Fs.open(filepath1, Fs.OpenMode.Create);
        Fs.close(file);
    }
    { // copy
        err :: Fs.copy(filepath1, filepath2);
        test_ok(err);
        test_true(Fs.exist(filepath1));
        test_true(Fs.exist(filepath2));
    }
    { // copy 2
        err :: Fs.copy(filepath1, filepath2, false);
        test_not_ok(err);
        test_true(Fs.exist(filepath1));
        test_true(Fs.exist(filepath2));
    }

    Fs.remove(filepath1);
    Fs.remove(filepath2);
}

fs_list_dir_test1 :: fn () #test {
    dir :: Fs.get_cwd();
    defer String.delete(dir);
    list, err :: Fs.dir_scan(dir);
    defer Fs.info_list_delete(list);
    test_ok(err);
    test_true(list.len > 0);
    loop i := 0; i < list.len; i += 1 {
        test_neq(list[i].name, String.empty);
    }
}

fs_list_dir_test2 :: fn () #test {
    dir :: Fs.get_cwd();
    defer String.delete(dir);
    list, err :: Fs.dir_scan(dir, &fn (info: *Fs.Info) bool {
        return info.kind == Fs.InfoKind.Directory;
    });
    defer Fs.info_list_delete(list);
    test_ok(err);
    test_true(list.len > 0);
    loop i := 0; i < list.len; i += 1 {
        test_neq(list[i].name, String.empty);
        test_true(list[i].kind == Fs.InfoKind.Directory);
    }
}

fs_list_dir_test3 :: fn () #test {
    dir :: Fs.get_cwd();
    defer String.delete(dir);
    list, err :: Fs.dir_scan(dir, &fn (info: *Fs.Info) bool {
        return info.kind == Fs.InfoKind.File;
    });
    defer Fs.info_list_delete(list);
    test_ok(err);
    test_true(list.len > 0);
    loop i := 0; i < list.len; i += 1 {
        test_neq(list[i].name, String.empty);
        test_true(list[i].kind == Fs.InfoKind.File);
    }
}

fs_is_directory_test1 :: fn () #test {
    tmp :: Fs.get_tmp();
    defer String.delete(tmp);
    state, err :: Fs.is_directory(tmp);
    test_ok(err);
    test_true(state);
}

fs_is_directory_test2 :: fn () #test {
    state, err :: Fs.is_directory(#file);
    test_ok(err);
    test_false(state);
}

fs_dir_create_remove_test1 :: fn () #test {
    dir :: Fs.get_tmp();
    defer String.delete(dir);
    String.append(&dir, "/__DELETE_ME__");

    Fs.dir_remove_all(dir);
    state1 :: Fs.dir_create(dir);
    test_ok(state1);

    state2 :: Fs.dir_create(dir);
    test_not_ok(state1);

    state3 :: Fs.dir_remove(dir);
    test_ok(state3);
}

fs_dir_remove_all_test1 :: fn () #test {
    dir :: Fs.get_tmp();
    defer String.delete(dir);
    String.append(&dir, "/__DELETE_ME__");
    
    Fs.dir_remove_all(dir);
    {
        s :: Fs.dir_create(dir);
        test_ok(s);
    }
    
    {
        file :: String.new(dir);
        defer String.delete(file);
        String.append(&file, "/file.txt");
        f, e :: Fs.open(file, Fs.OpenMode.Create);
        test_ok(e);
        Fs.close(f);
    }
    
    state :: Fs.dir_remove_all(dir);
    test_ok(state);
    test_false(Fs.exist(dir));
}

fs_dir_create_all_test1 :: fn () #test {
    dir :: Fs.get_tmp();
    defer String.delete(dir);
    String.append(&dir, "/foo");
    Fs.dir_remove_all(dir);
    String.append(&dir, "/bar/bazz");
    err :: Fs.dir_create_all(dir);
    test_ok(err);
}