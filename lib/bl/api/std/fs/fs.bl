//! ===========
//! File System
//! ===========
//! 
//! ::
//! 
//!    #import "std/fs"
//! 
//! File system module for manipulation with files and directories. 
//! This module provides an abstraction over operating system APIs such as 
//! creating files and directories, reading and writing, and scanning the filesystem.

#scope Fs

/// File handle type.
File :: _FsImpl.File;

/// Invalid file handle. This can be used for file handle validation.
FILE_INVALID :: _FsImpl.FILE_INVALID;

/// Specify operation with opened file.
OpenMode :: enum {
    /// Open file for reading.
    Read;
    /// Open file for writing and truncate its content.
    Write;
    /// Open file for appending (keeps current content).
    Append;
    /// Create file if it does not exist.
    Create;
}

OpenOpt :: struct {
    /// Open file for reading.
    read: bool;
    /// Open file for writing and truncate its content.
    write: bool;
    /// Open file for appending (keeps current content).
    append: bool;
    /// Create file if it does not exist.
    create: bool;
}

/// Specify behavior of :ref:`Fs.dir_copy`.
CopyOpt :: struct {
    /// Copy all directories and sub-directories.
    recursive: bool;
    /// Override existing entries.
    override: bool;
    /// Skip already existing entries.
    skip_existing: bool;
}

/// Specify kind of file system entry.
InfoKind :: enum {
    File;
    Directory;
}

/// Helper container to hold information about file system entry.
Info :: struct {
    kind: InfoKind;
    name: string;
}

InfoList :: []Info;

/// Check whether file or directory exists.
exist :: fn (filepath: string) bool #inline {
    return _FsImpl.exist(filepath);
}

/// Open an file specified by `filepath`. Function return file handle and `OK` status when
/// file was openned, otherwise return invalid handle and proper error. File must be closed
/// by :ref:`Fs.close` call.
///
/// File open `mode` is optional, any combination of :ref:`OpenMode` can be used.
/// When `Create` `mode` is specified, new file is created on `filepath` only if it does
/// not exist, otherwise already existing file is used. `Read` `mode` is used as default 
/// when nither `Read`, `Write` or `Append` is specified.
///
/// Example
/// -------
/// .. literalinclude:: /examples/docs/004.bl
///    :language: bl
open :: fn (filepath: string, mode: ...OpenMode) (File, Error) #inline {
    return _FsImpl.open(filepath, mode);
}

/// Close previously openned file. Does nothing when `handle` is not valid.
close :: fn (handle: File) #inline {
    _FsImpl.close(handle);
}

/// @INCOMPLETE
get_uid :: fn { _FsImpl.get_uid; _FsImpl.get_uid_by_name; }

/// Try to remove file specified by `filepath` and return `OK` on success, otherwise
/// return error.
remove :: fn (filepath: string) Error #inline {
    return _FsImpl.remove(filepath);
}

/// Copy existing file from `src` to `dest` and override existing file in destination
/// if `override` is true. Return `ok` or `error`.
copy :: fn (src: string, dest: string, override := false) Error #inline {
    return _FsImpl.copy(src, dest, override);
}

/// Copy from `src` path to `dest` path with specified `opt` options and return
/// count of processed files or error.
///
/// Example:
///
/// .. code-block:: bl
///     
///     #import "std/fs"
///     
///     main :: fn () s32 {
///         // Copy options
///         opt: Fs.CopyOpt;
///         // Create destination directory if not exist.
///         opt.recursive = true;
///         // Override all existing entries.
///         opt.override = true;
///     
///         // Copy content of 'foo' into 'bar'
///         c, err :: Fs.dir_copy("foo", "bar", &opt, &fn (item: *Fs.Info) bool {
///             // Filter only txt files
///             if String.compare(Fs.get_extension(item.name), "txt") { return true; } 
///             return false;
///         });
///         if !is_ok(err) { print("%\n", err); }
///         else { print("Copied % files!\n", c); }
///         return 0;
///     }
///
dir_copy :: fn (src: string, dest: string, opt: *CopyOpt = &COPY_OPT_DEFAULT, filter: DirScanFilterFn = &_default_file_filter) (s64, Error) {
    cc: s64;
    if !opt                   { return 0, error(ERR_INVALID_HANDLE, "Invalid copy options!"); }
    if String.is_empty(src)   { return 0, error(ERR_INVALID_HANDLE, "Source path is empty!"); }
    if String.is_empty(dest)  { return 0, error(ERR_INVALID_HANDLE, "Destination path is empty!"); }
    if !_FsImpl.exist(src)   { return 0, error(ERR_NOT_FOUND, "Source path '%' not exist!", src); }
    if !_FsImpl.exist(dest) {
        if opt.recursive {
            err :: _FsImpl.dir_create(dest);
            if !is_ok(err) { return 0, err; }
        } else {
            return 0, error(ERR_NOT_FOUND, "Destination path '%' not exist!", dest);
        }
    }
    is_src_valid := _FsImpl.is_directory(src);
    if !is_src_valid { return 0, error("Source '%' is not directory!", src); }
    is_dest_valid := _FsImpl.is_directory(dest);
    if !is_dest_valid { return 0, error("Destination '%' is not directory!", dest); }

    dest_path :: String.new(128);
    src_path :: String.new(128);
    defer String.delete(dest_path);
    defer String.delete(src_path);
    String.clear_concat(&dest_path, dest, "/", src); 
    if _FsImpl.exist(dest_path) { return 0, error("Destination directory '%' already exist!", dest_path); }
    list, list_err :: _FsImpl.dir_scan(src, filter);
    if !is_ok(list_err) { return 0, list_err; }
    defer info_list_delete(list);
    loop i := 0; i < list.len; i += 1 {
        copy_err: Error;
        _cc: s64;
        String.clear_concat(&src_path, src, "/", list[i].name);
        String.clear_concat(&dest_path, dest, "/", list[i].name);
        if opt.skip_existing && exist(dest_path) { continue; }
        switch list[i].kind {
            InfoKind.File {
                copy_err = _FsImpl.copy(src_path, dest_path, opt.override);
                _cc = 1;
            }

            InfoKind.Directory {
                c, err :: dir_copy(src_path, dest_path, opt);
                copy_err = err;
                _cc = c;
            }
        }
        if !is_ok(copy_err) { return 0, copy_err; }
        cc += _cc;
    }
    return cc, ok();
}

/// Return file content size in bytes and `OK` status on success, otherwise
/// return zero and proper error.
size :: fn (handle: File) (usize, Error) #inline {
    return _FsImpl.size(handle);
}

/// Read whole file content into string. Return new string instance containting
/// file data and `OK` status on success, otherwise return empty string and error.
/// Returned string is expected to be released by :ref:`String.delete` call if
/// there was no error reported by function. Result string is zero terminated even
/// if file is empty.
///
/// Example
/// -------
/// .. literalinclude:: /examples/docs/005.bl
///    :language: bl
read_string :: fn (handle: File) (string, Error) {
    size, err1 := _FsImpl.size(handle);
    if !is_ok(err1) {
        return String.empty, err1;
    }
    size += 1; // To handle zero terminator.
    buf := String.new(cast(usize) size);
    read, err2 :: _FsImpl.read(handle, buf.ptr, auto size - 1);
    if !is_ok(err2) {
        String.delete(buf);
        return String.empty, err2;                
    }
    buf.len = auto read + 1;
    buf[buf.len-1] = '\0';
    buf.len -= 1;
    return buf, ok();
}

/// Read whole file content into slice array. Return new slice instance containting
/// file data and `OK` status on success, otherwise return empty slice and error.
/// Returned slice is expected to be released by :ref:`slice_terminate` call if
/// there was no error reported by function.
read_slice :: fn (handle: File) ([]u8, Error) {
    invalid: []u8;
    size, err :: _FsImpl.size(handle);
    if !is_ok(err) {
        return invalid, err;
    }
    buf: []u8;
    if size == 0 { return buf, ok(); }
    slice_init(&buf, auto size);
    read, err2 :: _FsImpl.read(handle, buf.ptr, auto size);
    if !is_ok(err2) {
        slice_terminate(&buf);
        return invalid, err2;                
    }
    buf.len = read;
    return buf, ok();
}


/// Read `size` bytes from file into raw `dest` buffer. Return count of bytes
/// written into buffer and `OK` status when there was no error. `dest` buffer
/// must be allocated to handle at least `size` bytes. There is no overflow
/// check.
read :: fn (handle: File, dest: *u8, size: s64) (s64, Error) #inline {
    return _FsImpl.read(handle, dest, size);
}

/// Write `size` bytes from raw buffer `src` into the file. Return count of
/// bytes written into the buffer and `OK` on success, otherwise return error.
/// `src` buffer size must be at least `size` bytes. No overflow checking
/// is done.
write :: fn (handle: File, src: *u8, size: s64) (s64, Error) #inline {
    return _FsImpl.write(handle, src, size);
}

/// Write content of `str` into the file, return count of written bytes and `OK` if
/// there is no error, otherwise return 0 and error.
write_string :: fn (handle: File, str: string) (s64, Error) #inline {
    written_bytes, err :: _FsImpl.write(handle, str.ptr, str.len);
    if !is_ok(err) { return 0, err; }
    return auto written_bytes, ok();    
}

// @INCOMPLETE
write_fmt :: fn {
    fn (buf_size: s32, handle: File, fmt: string, args: ...) (s64, Error) #inline {
        if buf_size <= 0 { return 0, ok(); }
        buf: []u8;
        buf.ptr = alloc(sizeof(u8) * auto buf_size);
        buf.len = auto buf_size;
        defer free(buf.ptr);
        len :: bprint(buf, fmt, args);
        return write_slice(handle, buf, len);
    };
    fn (handle: File, fmt: string, args: ...) (s64, Error) #inline {
        buf: [1024]u8 #noinit;
        len :: bprint(buf, fmt, args);
        return write_slice(handle, buf, len);
    };
}

/// Write content of `v` slice into the file, return count of written bytes and `OK` if
/// there is no error, otherwise return 0 and error. Optional argument `count` specify
/// count of bytes from `v` to be written, negative value means whole content. When
/// passed count is greater than `v.len`, count value is set to `v.len`.
write_slice :: fn (handle: File, v: []u8, count : s64 = -1) (s64, Error) #inline {
    if count < 0 { count = v.len; } 
    if count > v.len { count = v.len; }
    written_bytes, err :: _FsImpl.write(handle, v.ptr, count);
    if !is_ok(err) { return 0, err; }
    return auto written_bytes, ok();
}

/// Create new directory and return `OK` on success. This function does not
/// create directories recursively.
dir_create :: fn (dirpath: string) Error #inline {
    return _FsImpl.dir_create(dirpath);
}

dir_create_all :: fn (dirpath: string) Error {
    if String.is_empty(dirpath) { return error(ERR_INVALID_HANDLE, "Dir path is empty!"); }
    if _FsImpl.exist(dirpath) { return ok(); } 
    nodes :: String.split_by(dirpath, '/');
    defer slice_terminate(&nodes);
    path :: String.new(auto dirpath.len);
    defer String.delete(path);
    loop i := 0; i < nodes.len; i += 1 {
        if nodes[i].len == 0 { continue; }
        String.append(&path, nodes[i]);
        String.append(&path, "/");
        if _FsImpl.exist(path) { continue; } 
        err :: _FsImpl.dir_create(path);
        if !is_ok(err) { return err; }
    }
    return ok();
}

/// Remove directory specified by `dirpath` and return `OK` on success,
/// otherwise return an error.
dir_remove :: fn (dirpath: string) Error #inline {
    return _FsImpl.dir_remove(dirpath);
}

/// Remove non-empty directory specified by `dirpath` and return `OK` on success,
/// otherwise return an error. Root directory is removed if `remove_root` is `true`.
/// Custom file `filter` function can be specified as needed.
///
/// .. note:: This function recursively remove all nested folders and files in specified
///           sub tree so it can be expensive.
dir_remove_all :: fn (dirpath: string, remove_root := true, filter: DirScanFilterFn = &_default_file_filter) Error {
    if String.is_empty(dirpath) { return error(ERR_INVALID_HANDLE, "Dir path is empty!"); }
    if !filter { return error(ERR_INVALID_HANDLE, "Invalid list filter!"); }
    tmp :: String.new(cast(usize) dirpath.len);
    defer String.delete(tmp);
    list, err :: _FsImpl.dir_scan(dirpath, filter);
    if !is_ok(err) { return err; }
    defer info_list_delete(list);
    loop i := 0; i < list.len; i += 1 {
        node :: &list[i];
        String.concatenate(&tmp, dirpath, "/", node.name);
        switch node.kind {
            InfoKind.Directory {
                err :: dir_remove_all(tmp);
                if !is_ok(err) { return err; }
            }            
            InfoKind.File {
                err :: _FsImpl.remove(tmp);
                if !is_ok(err) { return err; }
            }
        }
        String.clear(&tmp);
    }
    if remove_root { return _FsImpl.dir_remove(dirpath); }
    return ok();
}

/// Check whether `path` points to valid directory and return true with `OK` state,
/// otherwise return `false` and error.
is_directory :: fn (path: string) (bool, Error) #inline {
    return _FsImpl.is_directory(path);
}

/// Type of `dir_scan` filter function.
DirScanFilterFn :: *fn(info: *Info) bool;

/// Scan `dirpath` directory and return list of information for every file system
/// entry found on success. Otherwise return empty list and error. Use :ref:`Fs.info_list_delete
/// to release list when there was no error reported by this function.
///
/// Optional `filter` funtion :ref:`Fs.DirScanFilterFn` can be used to filter scan results
/// directly during scanning, it's called for every found entry and only those for
/// whose filter yealds true are added into the output list.
dir_scan :: fn (dirpath: string, filter: DirScanFilterFn = &_default_file_filter) (InfoList, Error) #inline {
    return _FsImpl.dir_scan(dirpath, filter);
}

/// Release allocated file system entry info list.
info_list_delete :: fn (list: InfoList) #inline {
    loop i := 0; i < list.len; i += 1 {
        String.delete(list[i].name);
    }
    slice_terminate(&list);
}

/// Checks whether `name` is valid file name on current platform.
validate_filename :: fn (name: string) bool #inline {
    return _FsImpl.validate_filename(name);
}

/// Try to obtain current working directory, result must be released by
/// :ref:`String.delete`. Path does not contain last path separator.
get_cwd :: fn () string #inline {
    return _FsImpl.get_cwd();
}

/// Sets current working directory and return `OK` on success, otherwise
/// return error.
set_cwd :: fn (path: string) Error #inline {
    return _FsImpl.set_cwd(path);
}

/// Try to obtain system home directory, result must be released by
/// :ref:`String.delete`. Path does not contain last path separator.
get_home :: fn () string #inline {
    return _FsImpl.get_home();
}

/// Try to obtain system temporary directory, result must be released by
/// :ref:`String.delete`. Path does not contain last path separator.
get_tmp :: fn () string #inline {
    return _FsImpl.get_tmp();
}

/// Try to normalize `filepath`, basically try to remove all relative path
/// nodes `..` and `.`. Path must be valid path (existing) on system. Original
/// `filepath` is extended with current working directory. Function return `OK`
/// on success or propper error on fail. Original string is not modified when
/// error occurs.
normalize :: fn (filepath: *string) Error #inline {
    return _FsImpl.normalize(filepath);
}

/// Remove file extension (token after first `.`) from the `filename` and
/// return the rest. Function does not create copy of original `filepath`.
remove_extension :: fn (filename: string) string #inline {
    return _FsImpl.remove_extension(filename);
}

/// Get file extension (token after first `.`) from the `filename`.
/// Function does not create copy of original `filepath`.
/// The `.` separator is not included into result string.
get_extension :: fn (filename: string) string #inline {
    return _FsImpl.get_extension(filename);
}

#private
COPY_OPT_DEFAULT :: {:CopyOpt: 0};

_default_file_filter :: fn (item: *Info) bool {
    return String.compare(item.name, ".") == false
        && String.compare(item.name, "..") == false;
}