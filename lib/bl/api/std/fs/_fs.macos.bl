#load "os/macos/stat.bl"
#load "os/macos/fcntl.bl"
#load "os/macos/dirent.bl"
#load "os/posix/unistd.bl"
#load "fs.bl"

#scope _fs_impl

/// File handle type.
File :: s64;
FILE_INVALID : File : -1;

exist :: fn (filepath: string) bool #inline {
    if filepath.len == 0 { return false; }
    tmp: _stat_t;
    return _stat(auto filepath.ptr, &tmp) == 0;
}

open :: fn (filepath: string, mode: ...fs.OpenMode) (fs.File, Error) {
    if strings.is_empty(filepath) { return 0, error(ERR_INVALID_HANDLE, "File path is empty!"); }
    flags := modes_to_flags(mode);
    mode_flags :: _S_IRUSR | _S_IWUSR | _S_IROTH | _S_IWOTH;
    handle :: _open(auto filepath.ptr, flags, mode_flags);
    if handle < 0 { // error
        ec, estr :: os_get_last_error();
        return 0, error(ec, "Cannot open file '%': %.", filepath, estr);
    }    
    return handle, ok();   
}

remove :: fn (filepath: string) Error #inline {
    if strings.is_empty(filepath) { return error(ERR_INVALID_HANDLE, "File path is empty!"); }
    if _unlink(auto filepath.ptr) != 0 {
        ec, estr :: os_get_last_error();
        return error(ec, "Cannot remove file '%': %.", filepath, estr);
    }
    return ok();
}

get_uid :: fn (handle: fs.File) (u64, Error) {
    tmp: _stat_t;
    if _fstat(auto handle, &tmp) != 0 {
        ec, estr :: os_get_last_error();
        return 0, error(ec, "Cannot get file information.", estr);
    }
    return auto tmp.st_ino, ok();
}

get_uid_by_name :: fn (filepath: string) (u64, Error) {
    file, err1 :: open(filepath);
    defer close(file);
    if !is_ok(err1) { return 0, err1; }
    return get_uid(file);
}

copy :: fn (src: string, dest: string, override: bool) Error {
    if strings.is_empty(src) { return error(ERR_INVALID_HANDLE, "File path is empty!"); }
    if strings.is_empty(dest) { return error(ERR_INVALID_HANDLE, "File path is empty!"); }
    if exist(dest) && !override { return error(ERR_ALREADY_EXIST, "Destination already exist '%'", dest); }
    file_src, err1 :: open(src, fs.OpenMode.Read);
    defer close(file_src);
    if !is_ok(err1) { return err1; }
    tmp, err2 :: fs.read_slice(file_src);
    defer slice_terminate(tmp);
    if !is_ok(err2) { return err2; }
    file_dest, err3:: open(dest, fs.OpenMode.Write, fs.OpenMode.Create);
    defer close(file_dest);
    if !is_ok(err3) { return err3; }
    _, err4 :: fs.write_slice(file_dest, tmp);
    return ok();
}

close :: fn (handle: fs.File) #inline {
    _close(auto handle);
}

size :: fn (handle: fs.File) (usize, Error) #inline {
    tmp: _stat_t;
    if _fstat(auto handle, &tmp) != 0 {
        ec, estr :: os_get_last_error();
        return 0, error(ec, "Cannot get file size: %.", estr);
    }
    return auto tmp.st_size, ok();
}

read :: fn (handle: fs.File, dest: *u8, size: s64) (s64, Error) {
    if !dest { return 0, error(ERR_INVALID_HANDLE, "Cannot read, destination is invalid."); }
    read :: _read(auto handle, auto dest, auto size);
    if read < 0 { 
        ec, estr :: os_get_last_error();
        return 0, error(ec, "Cannot read file: %", estr);
    }
    return auto read, ok();
}

write :: fn (handle: fs.File, src: *u8, size: s64) (s64, Error) {
    if !src { return 0, error(ERR_INVALID_HANDLE, "Cannot write, source is invalid."); }
    written :: _write(auto handle, auto src, auto size);
    if written < 0 { 
        ec, estr :: os_get_last_error();
        return 0, error(ec, "Cannot write to file: %.", estr);
    }
    return auto written, ok();
}

dir_create :: fn (dirpath: string) Error {
    if strings.is_empty(dirpath) { return error(ERR_INVALID_HANDLE, "Cannot create directory, dirpath is empty."); }
    if _mkdir(auto dirpath.ptr, _S_IRWXU | _S_IRWXG | _S_IROTH | _S_IXOTH) != 0 {
        ec, estr :: os_get_last_error();
        return error(ec, "Cannot create directory '%': %.", dirpath, estr);
    }
    return ok();
}

dir_remove :: fn (dirpath: string) Error {
    if strings.is_empty(dirpath) { return error(ERR_INVALID_HANDLE, "Cannot remove directory, dirpath is empty."); }
    if _rmdir(auto dirpath.ptr)!= 0 {
        ec, estr :: os_get_last_error();
        return error(ec, "Cannot remove directory '%': %.", dirpath, estr);
    }
    return ok();
}

dir_scan :: fn (dirpath: string, filter: fs.DirScanFilterFn) (fs.InfoList, Error) {
    invalid: fs.InfoList;
    dir := _opendir(auto dirpath.ptr);
    if !dir {
        ec, estr :: os_get_last_error();
        return invalid, error(ec, "Cannot list directory '%': %.", dirpath, estr);
    }
    defer _closedir(dir);

    fc := 0;
    loop _readdir(dir) != null { fc += 1; }
    _rewinddir(dir);
    defer _rewinddir(dir);

    buf: fs.InfoList;
    slice_init(buf, fc);
    if fc == 0 { return buf, ok(); }

    buf.len = 0;
    loop {
        stat :: _readdir(dir);
        if stat == null { break; }
        kind: fs.InfoKind;
        name: string;
        if stat.d_type == auto _DT_DIR {
            kind = fs.InfoKind.Directory;
            name = strings.new(cast(*u8) stat.d_name.ptr);
        } else {
            kind = fs.InfoKind.File;
            name = strings.new(cast(*u8) stat.d_name.ptr);
        }
        push := true;
        info :: {:fs.Info: kind, name};
        if filter { push = filter(&info); }
        if push {
            buf.len += 1;
            buf[buf.len-1] = info;
        } else {
            strings.delete(name);
        }
    }
    return buf, ok();
}

is_directory :: fn (path: string) (bool, Error) #inline {
    if strings.is_empty(path) { return false, error(ERR_INVALID_HANDLE, "Path is empty."); }
    stat: _stat_t #noinit;
    if _stat(auto path.ptr, &stat) != 0 {
        ec, estr :: os_get_last_error();
        return false, error(ec, "Cannot check directory '%': %.", path, estr);
    }
    return _S_ISDIR(stat.st_mode), ok();
}

validate_filename :: fn (name: string) bool {
    if name.len == 0 { return false; }
    invalid :: "/";
    loop i := 0; i < invalid.len; i += 1 {
        if strings.count_of(name, invalid[i]) > 0 { return false; }
    }
    return true;
}

get_cwd :: fn () string #inline {
    buf: [OS_PATH_MAX]u8;
    if !_getcwd(auto buf.ptr, auto buf.len) { return strings.new(); }
    path :: strings.new(buf.ptr);
    return remove_last_separator(path);
}

set_cwd :: fn (path: string) Error #inline {
    if strings.is_empty(path) { return error(ERR_INVALID_HANDLE, "Path is empty!"); }
    if _chdir(auto path.ptr) != 0 {
        ec, estr :: os_get_last_error();
        return error(ec, "Cannot set current directory to '%': %.", path, estr);
    }
    return ok();
}

get_home :: fn () string #inline {
    tmp :: "HOME";
    cdir :: cast(*u8) c_getenv(auto tmp.ptr);
    if !cdir { return strings.new(); }
    path :: strings.new(cdir);
    return remove_last_separator(path);
}

get_tmp :: fn () string #inline {
    tmp :: "TMPDIR";
    cdir :: cast(*u8) c_getenv(auto tmp.ptr);
    if !cdir { return strings.new("/tmp"); }
    path :: strings.new(cdir);
    return remove_last_separator(path);
}

normalize :: fn (filepath: *string) Error {
    if !filepath { return error(ERR_INVALID_HANDLE, "File path is null!"); }
    if strings.is_empty(^filepath) { return error(ERR_INVALID_HANDLE, "File path is empty!"); }
    buf : [OS_PATH_MAX]u8;
    in_buf : [OS_PATH_MAX]u8;

    if !is_path_absolute(^filepath) {
        cwd :: get_cwd();
        defer strings.delete(cwd);
        bprint(in_buf, "%/%", cwd, ^filepath);
    } else {
        bprint(in_buf, "%", ^filepath);
    }
    if !c_realpath(auto in_buf.ptr, auto buf.ptr) {
        ec, estr :: os_get_last_error();
        return error(ec, "Cannot normalize '%': %.", ^filepath, estr);
    }
    strings.clear(filepath);
    strings.append(filepath, cstr_to_str(buf.ptr));
    return ok();
}

remove_extension :: fn (filename: string) string #inline {
    if strings.is_empty(filename) { return strings.empty; }
    tmp := filename;
    tmp.len = 0;
    loop i := 0; i < filename.len; i += 1 {
        c :: filename[i];
        if c == '.' && i > 0 { break; }
        tmp.len += 1;
    }
    return tmp;
}

get_extension :: fn (filename: string) string #inline {
    if strings.is_empty(filename) { return strings.empty; }
    if filename[0] == '.' {
        if filename.len == 1 { return strings.empty; }
        filename.len -= 1; filename.ptr = &filename[1]; 
    }
    rhs: string;
    if strings.split_by_first(filename, '.', null, &rhs) {
        return rhs;
    }
    return strings.empty;
}

#private
modes_to_flags :: fn (modes: []fs.OpenMode) s32 #inline {
    r, w, create, append := false;
    loop i := 0; i < modes.len; i += 1 {
        switch modes[i] {
            fs.OpenMode.Read    { r = true; }            
            fs.OpenMode.Write   { w = true; }
            fs.OpenMode.Append  { w = true; append = true; }
            fs.OpenMode.Create  { create = true; }
            default { panic(); }
        }
    }
    flags: s32;
    if ((!w) && (!r)) { flags = _O_RDONLY; }
    else if (w && r) { flags = _O_RDWR; }
    else if w { flags = _O_WRONLY; }
    else if r { flags = _O_RDONLY; }
    if create { flags = flags | _O_CREAT; }
    if append { flags = flags | _O_APPEND; }
    if w && !append { flags = flags | _O_TRUNC; }
    return flags;
}

remove_last_separator :: fn (path: string) string {
    if strings.is_empty(path) { return path; }
    if path[path.len-1] == '/' {
        path[path.len-1] = '\0';
        path.len -= 1;
    }
    return path;
}

is_path_absolute :: fn (path: string) bool #inline {
    return path.len > 0 && path[0] == '/';
}