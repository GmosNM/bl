#load "thread.bl"

#scope _thread_impl

Handle :: win32.HANDLE;

create :: fn (entry: std.ThreadEntryFn, ctx: *u8) (Handle, Error) {
    if !entry { return null, error(ERR_INVALID_HANDLE, "Cannot create thread. Handle is invalid."); }
    thread :: win32.CreateThread(null, 0, auto entry, auto ctx, 0, null);
    if !thread {
        ec, estr :: os_get_last_error();
        return null, error(ec, "Cannot create thread: %.", estr);
    }
    return thread, OK;
}

join :: fn (thread: Handle) (exit_code: s32, err: Error) {
    if !thread { error(ERR_INVALID_HANDLE, "Cannot join. Handle is invalid."); }
    if win32.WaitForSingleObject(thread, win32.INFINITE) == win32.WAIT_FAILED {
        ec, estr :: os_get_last_error();
        return -1, error(ec, "Cannot join: %.", estr);
    }
    exit_code: u32;
    if win32.GetExitCodeThread(thread, &exit_code) == 0 {
        ec, estr :: os_get_last_error();
        return -1, error(ec, "Cannot join: %.", estr);
    }
    if win32.CloseHandle(thread) == 0 {
        ec, estr :: os_get_last_error();
        return -1, error(ec, "Cannot join: %.", estr);
    }
    return auto exit_code, OK;
}

current :: fn () Handle #inline {
    return win32.GetCurrentThread();
}

current_id :: fn () u64 #inline {
    return auto win32.GetCurrentThreadId();
}

exit :: fn (exit_code: s32) Error {
    thread :: current();
    if win32.TerminateThread(thread, auto exit_code) == 1 {
        ec, estr :: os_get_last_error();
        return error(ec, "Cannot exit thread: %.", estr);
    }
    return OK;
}

