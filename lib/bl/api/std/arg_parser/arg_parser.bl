//! # Command Line Argument Parser
//! 
//! `#import "std/arg_parser"`
//!
//! Generic command line argument parsing tool.
//! 
//! ### Example
//! 
//! ```
//! #import "std/arg_parser"
//! 
//! // Command line arguments context.
//! Args :: struct #base ArgParser.Ctx {
//!     my_flag: bool;
//!     my_name: string;
//! }
//! 
//! g_args: Args;
//! 
//! main :: fn () s32 {
//!     parser :: ArgParser.new();
//!     defer ArgParser.delete(parser);
//!     // After this all positional arguments stored inside the argument parser
//!     // context are invalid.
//!     defer ArgParser.ctx_terminate(&g_args);
//! 
//!     // Add new argument.
//!     ArgParser.add(
//!         parser, 
//!         "-f", // Short name (must start with -)
//!         "--flag", // Long name (must start with -)
//!         "Specify my flag.", // Help text.
//!         &fn (parser: ArgParser.Parser, args: []string, ctx: *ArgParser.Ctx) (s32, Error) {
//!             a: *Args = auto ctx;
//!             a.my_flag = true;
//!             return 1, ok(); // Return number of parsed arguments and state.
//!         });
//! 
//!     ArgParser.add(
//!         parser,
//!         "-n",
//!         "--name",
//!         "Specify my name.",
//!         &fn (parser: ArgParser.Parser, args: []string, ctx: *ArgParser.Ctx) (s32, Error) {
//!             a: *Args = auto ctx;
//!             if (args.len < 2) {
//!                 return 0, error("Expected name!");
//!             }
//!             a.my_name = args[1];
//!             return 2, ok();
//!         });
//! 
//!     // Start parsing.
//!     state :: ArgParser.run(parser, command_line_arguments, &g_args, 1);
//!     if !is_ok(state) {
//!         print_err("%", state);
//!         ArgParser.print_help(parser);
//!         return 1;
//!     }
//! 
//!     print("%\n", g_args);
//!     return 0;
//! }
//! ```

#import "std/array"

#scope ArgParser

/// Arguments context base type.
Ctx :: struct {
    /// Contains other positional arguments not considered as options (i.e. input files).
    positional: [..]string;
    /// True when help was invoked implicitly.
    help: bool;
}

/// Argument parser opaque handle.
Parser :: *u8;

/// Argument parser callback function type. In context of [add](#argparseradd) this callback 
/// function provides current `parser`, rest of the input argument slice starting from currently 
/// captured argument name and custom argument context `ctx`. Expected return of such function is 
/// count of parsed arguments from `args` input (at least one for successfully parsed argument with 
/// no additional input) and status.
///
/// See also: [add](#argparseradd)
///
/// **todo**: Is reference to parent parser needed here?
HandlerFn :: *fn (parser: Parser, args: []string, ctx: *Ctx) (s32, Error);

/// Create new instance of parser with possibility to set custom help text. Use 
/// [delete](#argparserdelete) to release parser instance.
new :: fn (help_text := String.empty) Parser {
    parser := cast(*ParserImpl) alloc(sizeof(ParserImpl));
    parser.pad = 0;
    parser.help_text = String.new(help_text);
    array_init(&parser.entries);

    add(auto parser,
        "-h",
        "--help",
        "Print this help.",
        &fn (parser: Parser, args: []string, ctx: *Ctx) (s32, Error) {
            if ctx { ctx.help = true; }
            print_help(parser);
            return 1, ok();
        });

    return auto parser;
}

/// Delete parser instance.
delete :: fn (_parser: Parser) {
    parser: *ParserImpl = auto _parser;
    loop i := 0; i < parser.entries.len; i += 1 {
        entry_delete(&parser.entries[i]);
    }
    array_terminate(&parser.entries);
    String.delete(parser.help_text);
    free(auto parser);
}

/// Release 'positional' arguments array inside the context data. 
ctx_terminate :: fn (ctx: *Ctx) {
    assert(ctx);
    array_terminate(&ctx.positional);
}

/// Perform parsing on input `args` slice and provide `ctx` as context to the argument parsing
/// callbacks. `start` specify first element of `args` slice considered to be an argument.
/// See also: (command_line_arguments)[#argparsercommand_line_arguments] 
/// (**note**: first element contains name of the executable)
run :: fn (_parser: Parser, args: []string, ctx: *Ctx, start := 0) Error {
    assert(ctx);
    parser: *ParserImpl = auto _parser;
    if start >= args.len {
        return ok();
    } else {
        args.ptr = &args[start];
        args.len -= start;
    }

    loop args.len > 0 {
        arg :: &args[0];
        if arg.len > 0 && (@arg)[0] != '-' {
            array_push(&ctx.positional, @arg);
            args.ptr = &args[1];
            args.len -= 1;
            continue;
        }
        found: bool;
        loop e := 0; e < parser.entries.len; e += 1 {
            entry :: &parser.entries[e];
            if String.compare(@arg, entry.short) || String.compare(@arg, entry.long) {
                found = true;
                parsed, err :: entry.handler(_parser, args, ctx);
                if !is_ok(err) {
                    return err;
                }
                if parsed == 0 {
                    panic("Parsed count returned from argument lambda is supposed to be at least 1, not %!", parsed); 
                }
                if args.len > parsed { args.ptr = &args[parsed]; }
                args.len -= parsed;
                break;
            }
        }
        if !found {
            return error("Invalid argument '%'.", @arg);
        }
    }
    return ok();
}

/// Add new argument specified by `short` and `long` name to be recognised by parser. The argument 
/// description can be specified as `help`. `handler` function is called every time parser hits 
/// this argument (`short` or `long` name match). Argument name must be unique name starting with 
/// `-`. Help command (as `-h` or `--help`) is added by default.
///
/// ### Example
///
/// ```
/// ArgParser.add(parser,
///     "-d", // short name.
///     "--debug", // long name.
///     "Enable debug mode.", // Help text.
///     &fn (parser: ArgParser.Parser, args: []string, ctx: *ArgParser.Ctx) (s32, Error) {
///         a: *MyArgs = auto ctx;
///         a.is_debug = true;
///         return 1, ok(); // One argument parsed ('-d' or '--debug') 
///     });
/// ```
add :: fn (
    _parser: Parser,
    short: string,
    long: string,
    help: string,
    handler: HandlerFn
) {
    parser: *ParserImpl = auto _parser;
    entry: Entry #noinit;
    entry.short = String.new(short);
    entry.long = String.new(long);
    entry.help = String.new(help);
    entry.handler = handler;
    array_push(&parser.entries, entry);

    // '  short, long '
    pad :: 2 + short.len + 2 + long.len + 1;
    if parser.pad < pad { parser.pad = auto pad; }
}

/// Print help for all registered commands.
print_help :: fn (_parser: Parser) {
    parser: *ParserImpl = auto _parser;
    if !String.is_empty(parser.help_text) {
        print("%\n", parser.help_text);
    }
    print("Options:\n");
    loop i := 0; i < parser.entries.len; i += 1 {
        entry :: &parser.entries[i];
        printed: s32;
        if String.is_empty(entry.short) {
            printed = print("  % ", entry.long);
        } else {
            printed = print("  %, % ", entry.short, entry.long);
        }
        loop p := 0; p < (parser.pad - printed); p += 1 {
            print(" ");
        }
        print("%\n", entry.help);
    }
}

#private
ParserImpl :: struct {
    entries: [..]Entry;
    pad: s32;
    help_text: string; 
}

Entry :: struct {
    short: string;
    long: string;
    help: string;
    handler: HandlerFn;
}

entry_delete :: fn (entry: *Entry) {
    String.delete(entry.short);
    String.delete(entry.long);
    String.delete(entry.help);
}
