// =================================================================================================
// bl
//
// File:   test.bl 
// Author: Martin Dorazil
// Date:   2/11/19
//
// Copyright 2018 Martin Dorazil
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.
// =================================================================================================

#import "std/thread"
#import "std/string"
#import "std/array"

#load "std/debug.bl"

// @CLEANUP These functions can be rewritten as polymorphs.

//! # Test
//! 
//! `#load "std/test.bl"`
//! 
//! Integrated unit testing API.

/// ### Overloads
///
/// ```
/// fn (got: string, expected: string, loc := #call_location)
/// fn (got: f32, expected: f32, epsilon: f32 = Math.F32_EPSILON, loc := #call_location)
/// fn (got: f64, expected: f64, epsilon: f64 = Math.F64_EPSILON, loc := #call_location)
/// fn (got: ?T, expected: T, loc := #call_location)
/// ```
/// 
/// Test whether `got` value is equal to `expected` value.
test_eq :: fn {
    fn (got: string, expected: string, loc := #call_location) {
        if String.compare(expected, got) { return; }
        print_err("\nExpected: '%'\nGot:      '%'.", expected, got);
        stop(loc);
    };
    fn (got: f32, expected: f32, epsilon: f32 = Math.F32_EPSILON, loc := #call_location) {
        if Math.compare(got, expected, epsilon) { return; }
        print_err("\nExpected: '%'\nGot:      '%'.", expected, got);
        stop(loc);
    };
    fn (got: f64, expected: f64, epsilon: f64 = Math.F64_EPSILON, loc := #call_location) {
        if Math.compare(got, expected, epsilon) { return; }
        print_err("\nExpected: '%'\nGot:      '%'.", expected, got);
        stop(loc);
    };
    fn (got: ?T, expected: T, loc := #call_location) {
        if expected == got { return; }
        print_err("\nExpected: '%'\nGot:      '%'.", expected, got);
        stop(loc);
    };
}

/// ### Overloads
/// 
/// ```
/// fn (got: string, not_expected: string, loc := #call_location)
/// fn (got: f32, not_expected: f32, epsilon: f32 = Math.F32_EPSILON, loc := #call_location)
/// fn (got: f64, not_expected: f64, epsilon: f64 = Math.F64_EPSILON, loc := #call_location)
/// fn (got: ?T, not_expected: T, loc := #call_location)
/// ```
/// 
/// Test whether `got` value is not equal to `not_expected` value.
test_neq :: fn {
    fn (got: string, not_expected: string, loc := #call_location) {
        if !String.compare(not_expected, got) { return; }
        print_err("\nExpected not to be '%'.", not_expected);
        stop(loc);
    };
    fn (got: f32, not_expected: f32, epsilon: f32 = Math.F32_EPSILON, loc := #call_location) {
        if !Math.compare(got, not_expected, epsilon) { return; }
        print_err("Expected not to be '%'.", not_expected);
        stop(loc);
    };
    fn (got: f64, not_expected: f64, epsilon: f64 = Math.F64_EPSILON, loc := #call_location) {
        if !Math.compare(got, not_expected, epsilon) { return; }
        print_err("Expected not to be '%'.", not_expected);
        stop(loc);
    };
    fn (got: ?T, not_expected: T, loc := #call_location) {
        if got != not_expected { return; }
        print_err("Expected not to be '%'.", not_expected);
        stop(loc);
    };
}

/// Test whether `v1` is greater than `v2`
test_gt :: fn (v1: ?T, v2: T, loc := #call_location) {
    if v1 > v2 { return; }
    print_err("Value '%' is expected to be grater than '%'.", v1, v2);
    stop(loc);
}

/// Test whether `v1` is less than `v2`
test_lt :: fn (v1: ?T, v2: T, loc := #call_location) {
    if v1 < v2 { return; }
    print_err("Value '%' is expected to be less than '%'.", v1, v2);
    stop(loc);
}

/// Test whether `v` value is `true`.
test_true :: fn (v: bool, loc := #call_location) {
    if v { return; }
    print_err("Value expected to be true.");
    stop(loc);
}

/// Test whether `v` value is `false`.
test_false :: fn (v: bool, loc := #call_location) {
    if !v { return; }
    print_err("Value expected to be false.");
    stop(loc);
}

/// Test whether `ptr` pointer value is `null`.
test_null :: fn (ptr: *?T, loc := #call_location) {
    if ptr == null { return; }
    print_err("Pointer is expected to be null.");
    stop(loc);
}

/// Test whether `ptr` pointer value is not `null`.
test_not_null :: fn (ptr: *?T, loc := #call_location) {
    if ptr != null { return; }
    print_err("Pointer is expected not to be null.");
    stop(loc);
}

/// Test whether `err` is OK.
test_ok :: fn (err: Error, loc := #call_location) #inline {
    if is_ok(err) { return; }
    print_err("Expected to be OK not '%'.", err);
    stop(loc);
}

/// Test whether `err` is not OK.
test_not_ok :: fn (err: Error, loc := #call_location) #inline {
    if !is_ok(err) { return; }
    print_err("Expected not to be OK not '%'.", err);
    stop(loc);
}

/// Test whether `got_err` code is `expected_code`.
test_is_error :: fn (got_err: Error, expected_code: s32, loc := #call_location) #inline {
    if is_error(got_err, expected_code) { return; }
    if got_err {
        print_err(
            "\n"
            "Unexpected error:    '%'\n"
            "Expected error code: '%'\n"
            "Got:                 '%'.", got_err, expected_code, got_err.code);
    } else {
        print_err(
            "\n"
            "Unexpected error:    null\n"
            "Expected error code: '%'\n"
            "Got:                 null.", expected_code);
    } 
    stop(loc);
}

/// Execute all registered test cases in current assembly. The `test_run` function uses compiler 
/// builtin `testcases()` function returning slice of all registered test cases in the current 
/// assembly (all function with hash directive `#test`).
/// 
/// Test case execution can be used in runtime and compile time, in both cases function remap 
/// default behaviour of `panic()` function call and try to report all failing tests without 
/// execution termination.
/// 
/// Formatted output containing information about every test run status and summary report is 
/// produced during execution of this function. In case of test failure all needed information 
/// about fail source location is reported into standard output.
/// 
/// Returns number of failed tests.
/// 
/// ### Example
///     
/// ```
/// #load "std/test.bl"
/// 
/// my_test :: fn () #test {
///     print("Hello from test case!!!\n");     
/// }
/// 
/// main :: fn () s32 {
///     return test_run(); 
/// } 
/// ```
test_run :: fn () s32 {
    sighup :: fn (_: C.int) {};
    C.signal(C.SIGHUP, &sighup);

    cases := testcases();
    if cases.len == 0 {
        return 0;
    }
    print("\n");
    prev_abort_handler :: _context.abort_fn;
    defer _context.abort_fn = prev_abort_handler;

    _context.abort_fn = &abort_handler;
    _ci = 0;

    // setup cases
    array_init(&_queue);
    array_reserve(&_queue, cases.len);
    defer array_terminate(&_queue);

    print("\nTesting start\n");
    print("%\n", TEXT_LINE);

    loop i := 0; i < cases.len; i += 1 {
        array_push(&_queue, {:TestRun: cases[i].func, cases[i].name, false, 0.});
    }

    loop _ci < _queue.len {
        t :: Thread.create(&worker);
        Thread.join(t);
    }

    print("\nResults:\n");
    print("%\n", TEXT_LINE);

    fc: s64;
    loop i := 0; i < _queue.len; i += 1 {
        t :: &_queue[i];
        if t.passed { continue; }

        fc += 1;
        print("[      | FAIL ] % (% ms)\n", t.name, t.runtime_ms);
    }

    perc := 100;
    if fc > 0 { 
        perc = cast(s32) (cast(f32) (_queue.len - fc) / ((cast(f32) _queue.len) * 0.01f));
        print("%\n", TEXT_LINE);
    }

    // @HACK: Cannot print percent, escape sequence cannot be easily detected here.
    print("Executed: %, passed %%.\n", _queue.len, perc, "%");
    print("%\n", TEXT_LINE);
    return auto fc;
}

#private
TestRun :: struct {
    func: *fn();
    name: string;
    passed: bool;
    runtime_ms: f64;
}

TEXT_LINE :: "--------------------------------------------------------------------------------";
_queue: [..]TestRun;
_current: *TestRun;
_ci: s64;
_start_time: f64;

abort_handler :: fn () {
    _current.runtime_ms = os_tick_ms() - _start_time;
    _current.passed = false;
    print("[      | FAIL ] % (% ms)\n", _current.name, _current.runtime_ms);
    Thread.exit(1);
    __os_abort_default();
}

worker :: fn (_: *u8) s32 {
    loop _ci < _queue.len {
        _current = &_queue[_ci];
        _ci += 1; 
        _start_time = os_tick_ms();
        _current.func();
        _current.runtime_ms = os_tick_ms() - _start_time;
        _current.passed = true;
        print("[ PASS |      ] % (% ms)\n", _current.name, _current.runtime_ms);
    }
    return 0;
}

stop :: fn (loc: *CodeLocation) #inline {
     _context.print_log_fn(PrintLogKind.Error, loc.file, loc.line, "Test failed!");   
    unreachable;
}
