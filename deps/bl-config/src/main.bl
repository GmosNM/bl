#import "std/arg_parser"
#import "std/fs"

HELP_TEXT ::
"Platform specific bl configuration generator.\n"
"Usage:\n"
"  bl-config [options]";

LIB_DIR :: "../lib/bl/api";
ETC_DIR :: "../etc";
CONF_FILE :: "bl.conf";

// Command line arguments context.
Args :: struct #base ArgCtx {
    silent: bool;
    override_config: bool;
}

g_args: Args;
g_pwd: string;

main :: fn () s32 {
    _context.print_log_fn = &custom_logger;
    { // Setup pwd
        exec_path :: os_get_exec_path();
        defer string_delete(exec_path);
        dir: string;
        if string_split_by_last(exec_path, '/', &dir, null) {
            g_pwd = string_new(dir);
        } else {
            g_pwd = string_new(exec_path);
        }
    }
    defer string_delete(g_pwd);

    arg_parser :: arg_parser_new(HELP_TEXT);
    defer arg_parser_delete(arg_parser);
    
    arg_parser_add(
        arg_parser,
        "-s",
        "--silent",
        "Print only errors.",
        &fn (parser: ArgParser, args: []string, ctx: *ArgCtx) (s32, Error) {
            a: *Args = auto ctx;
            a.silent = true;
            return 1, ok();
        });
        
     arg_parser_add(
        arg_parser,
        "-f",
        "--force-override",
        "Override existing configuration file.",
        &fn (parser: ArgParser, args: []string, ctx: *ArgCtx) (s32, Error) {
            a: *Args = auto ctx;
            a.override_config = true;
            return 1, ok();
        });
    
        
    state: Error;
    state = arg_parser_run(arg_parser, command_line_arguments, &g_args, 1);
    if !is_ok(state) {
        print_err("%", state);
        arg_parser_print_help(arg_parser);
        return 1;
    }
    
    if g_args.help { return 0; }
    state = configure();
    if !is_ok(state) { 
        print_err("%", state);
        print_err("Configuration generation failed!");
        return 1; 
    }
    print_log("DONE.");
    return 0;
}

write_config :: fn (fmt: string, args: ...) Error {
    conf_file_path := sprint(g_pwd, "/", ETC_DIR);
    defer string_delete(conf_file_path);
    fs_dir_create(conf_file_path);
    string_concatenate(&conf_file_path, "/", CONF_FILE);
    if fs_exist(conf_file_path) {
        if !g_args.override_config {
            return error("Configuration file '%' already exists, use '-f' to override.", conf_file_path);
        }
    }
    file, err :: fs_open(conf_file_path, FSOpenMode.Create, FSOpenMode.Write);
    defer fs_close(file);
    if !is_ok(err) { return err; }
    fs_write_fmt(file, fmt, args);
    print_log("Configuration written to '%'.", conf_file_path);
    return ok();
}

#private
custom_logger :: fn (kind: PrintLogKind, file: string, line: s32, format: string, args: ...) {
    if g_args.silent && kind == PrintLogKind.Message { return; }
    switch kind {
        PrintLogKind.Message { print("  "); }
        PrintLogKind.Warning { print("  [W] "); }
        default { print("  [E] "); }                        
    }
    print(format, args);
    print("\n");
}
