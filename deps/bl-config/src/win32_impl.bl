#load "std/utils.bl"

BUILD_TOOLS :: "Microsoft Visual Studio/2019/BuildTools";
VC_VARS_ALL :: "VC/Auxiliary/Build/vcvarsall.bat";
LINKER_OPT_EXEC :: 
    "/NOLOGO /ENTRY:__os_start /SUBSYSTEM:CONSOLE /INCREMENTAL:NO /MACHINE:x64 "
    "kernel32.lib user32.lib gdi32.lib shell32.lib ucrt.lib legacy_stdio_definitions.lib msvcrt.lib vcruntime.lib Shlwapi.lib";

LINKER_OPT_SHARED :: 
    "/NOLOGO /INCREMENTAL:NO /MACHINE:x64 /DLL "
    "kernel32.lib user32.lib gdi32.lib shell32.lib ucrt.lib legacy_stdio_definitions.lib msvcrt.lib vcruntime.lib Shlwapi.lib";

CONFIG :: 
    "// BL configuration file\n"
    "// This file is generated by bl-config tool and used by 'blc' compiler during compilation\n"
    "// process.\n\n"
    "VERSION \"%\"\n"
    "// Main API directory containing all modules and source files.\n"
    "LIB_DIR \"%\"\n"
    "// Full path to Visual Studio vcvarsall.bat file.\n"
    "VC_VARS_ALL \"%\"\n"
    "// Default linker options used for executable linking.\n"
    "LINKER_OPT_EXEC \"%\"\n"
    "// Default linker options used for shared library linking.\n"
    "LINKER_OPT_SHARED \"%\"\n"
    "// Additional linker library path.\n"
    "LINKER_LIB_PATH \"\"\n"
    ;

popen :: fn (f: *c_char, m: *c_char) c_void_ptr #extern "_popen";
pclose :: fn (f: c_void_ptr) #extern "_pclose";

ArgsImpl :: struct #base ArgParser.Ctx {
    build_tools_path: string;
}
    
setup :: fn (arg_parser: ArgParser.Parser) {
     ArgParser.add(
        arg_parser,
        "-b",
        "--build-tools-path",
        "Specify custom MS Build Tools directory.",
        &fn (parser: ArgParser.Parser, args: []string, ctx: *ArgParser.Ctx) (s32, Error) {
            a := cast(*Args) ctx;
            if (args.len < 2) {
                return 0, error("Expected directory name!");
            }
            path := args[1];
            String.replace_all(&path, '\\', '/');
            if (!String.is_empty(path)) && path[path.len-1] == '\"' {
                path[path.len-1] = '\0';
                path.len -= 1;
            }
            a.build_tools_path = path;
            is_valid_directory :: Fs.is_directory(a.build_tools_path);
            if !is_valid_directory {  
                return 0, error("Invalid directory path '%'!", a.build_tools_path);
            }
            return 2, ok();
        });
}

configure :: fn () Error {
    print_log("Generate configuration file");
    program_files_path :: env_get("ProgramFiles(x86)");
    String.replace_all(&program_files_path, '\\', '/'); // oh my!
    build_tools_path: string;
    vc_vars_all : string;
    lib_dir: string; 
    linker_exec: string;
    defer String.delete(build_tools_path);
    defer String.delete(linker_exec);
    defer String.delete(lib_dir);
    defer String.delete(program_files_path);
    defer String.delete(vc_vars_all);
    
    // Lookup build tools instalation.
    if String.is_empty(g_args.build_tools_path) {
        build_tools_path = sprint(program_files_path, "/", BUILD_TOOLS);
    } else {
        build_tools_path = String.new(g_args.build_tools_path);
    }
    if !Fs.exist(build_tools_path) {
        return error(
            "Cannot find MS Build Tools installation on '%'. "
            "Download & install MS Build Tools from https://visualstudio.microsoft.com/visual-cpp-build-tools "
            "or specify custom location, see 'bl-config.exe -h' for more information", 
            build_tools_path);
    }
    print_log("MS Build Tools instalation on '%'.", build_tools_path);
    
    // Lookup vcvars bullshit
    vc_vars_all = sprint(build_tools_path, "/", VC_VARS_ALL);
    if !Fs.exist(vc_vars_all) {
        return error("Cannot find vcvarsall.bat on '%'!", vc_vars_all);
    }
    print_log("Found vcvarsall.bat on '%'.", vc_vars_all);
    
    lib_dir = sprint(g_pwd, "/", LIB_DIR);
    {
       err :: Fs.normalize(&lib_dir);
       if !is_ok(err) {
           return error("Cannot find BL API libs on '%'", lib_dir);
       }
    }
    print_log("Found BL API on '%'.", lib_dir);
    
    return write_config(
        CONFIG, 
        VERSION,
        lib_dir,
        vc_vars_all,
        LINKER_OPT_EXEC,
        LINKER_OPT_SHARED,
    );
}
